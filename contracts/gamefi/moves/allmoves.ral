import "std/nft_collection_interface"

// the nft collection contract for players
Contract AllMoves(
    collectionOwner: Address,
    mut collectionUri: ByteVec,
    gameContract: ByteVec,          // allows the battle to mint the item on completetion
    mut totalSupply: U256,    
    baseuri: ByteVec,
    playerContractId: ByteVec,
    // nft upgrades
    code: ByteVec,
    encodedImmutableFields: ByteVec,
    encodedMutableFields: ByteVec
) implements INFTCollection {
    event Mint(minter: Address, fromIndex: U256, contract: ByteVec)

    enum ErrorCodes {
        NFTNotFound = 0
        CollectionOwnerAllowedOnly = 1
        NFTNotPartOfCollection = 2
        NotValidNFT = 3
    }

    // base uri
    pub fn getCollectionUri() -> ByteVec {
        return collectionUri
    }

    // total supply at runtime
    pub fn totalSupply() -> U256 {
        return totalSupply
    }

    // get the nft by index
    @using(checkExternalCaller = false)
    pub fn nftByIndex(index: U256) -> INFT {
        let nftTokenId = subContractId!(toByteVec!(index))
        assert!(contractExists!(nftTokenId), ErrorCodes.NFTNotFound)

        return INFT(nftTokenId)
    }

    // validation ? ~ not being used
    @using(checkExternalCaller = false)
    pub fn validateNFT(nftId: ByteVec, index: U256) -> () {
      let expectedTokenContract = nftByIndex(index)
      assert!(nftId == contractId!(expectedTokenContract), ErrorCodes.NFTNotPartOfCollection)
    }

    // mint player
    @using(preapprovedAssets = true, checkExternalCaller = false, updateFields = true)
    pub fn mint(tokenSelected: ByteVec) -> () {

        burnToken!(callerAddress!(), tokenSelected, 1)

        // gets the index of the selected nft and then sets to the index of the new nft
        let nftUri = baseuri ++ u256ToString!(totalSupply)

        let c = #

        let (encodeImmutableFields, encodeMutableFields) = Player.encodeFields!(totalSupply, nftUri, selfContractId!(), gameContract, c, callerAddress!(), [1,1,1,1], [5,5,5,0], c, [c,c,c,c])

        let contractId = copyCreateSubContractWithToken!{callerAddress!() -> ALPH: minimalContractDeposit!()}(
            toByteVec!(totalSupply),
            playerContractId,
            encodeImmutableFields,
            encodeMutableFields,
            1,
            callerAddress!()
        )

        totalSupply = totalSupply + 1

        emit Mint(callerAddress!(), totalSupply, contractId)
    }

    // edit
    @using(checkExternalCaller = true, updateFields = true)
    pub fn editCollectionUri(newCollectionUri: ByteVec) -> () {
        checkCaller!(callerAddress!() == collectionOwner, ErrorCodes.CollectionOwnerAllowedOnly)

        collectionUri = newCollectionUri
    }

    // upgrades
    @using(checkExternalCaller = true)
    pub fn updateAllMoves(newCode: ByteVec) -> () {
        checkCaller!(callerAddress!() == collectionOwner, ErrorCodes.CollectionOwnerAllowedOnly)

        migrate!(newCode)
    }

    @using(checkExternalCaller = true, updateFields = true)
    pub fn updateAllMovesFields(newCode: ByteVec, newImmFieldsEncoded: ByteVec, newMutFieldsEncoded: ByteVec) -> () {
        checkCaller!(callerAddress!() == collectionOwner, ErrorCodes.CollectionOwnerAllowedOnly)

        migrateWithFields!(newCode, newImmFieldsEncoded, newMutFieldsEncoded)
    }

    // upgrade nfts
    @using(checkExternalCaller = false, preapprovedAssets =  true)
    pub fn updateMoveCode(nft: ByteVec) -> () {
        // get the data associated with the NFT
        let (id, tempIndex) = INFT(nft).getCollectionIndex()

        assert!(id == selfContractId!(), ErrorCodes.NotValidNFT)

        // verify that the nft is legitmate
        INFTCollection(id).validateNFT(nft, tempIndex)

        Player(nft).updatePlayerCode(code)
    }

    @using(checkExternalCaller = false, preapprovedAssets =  true)
    pub fn updateMoveFields(nft: ByteVec) -> () {
        // get the data associated with the NFT
        let (id, tempIndex) = INFT(nft).getCollectionIndex()

        assert!(id == selfContractId!(), ErrorCodes.NotValidNFT)

        // verify that the nft is legitmate
        INFTCollection(id).validateNFT(nft, tempIndex)

        Player(nft).updatePlayerFields(code, encodedImmutableFields, encodedMutableFields)
    }

}