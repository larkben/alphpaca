Contract BattleFactory(
    admin: Address,
    pvp: ByteVec,
    oracle: IDIAOracle,
    gameFi: ByteVec,
    // reward flow
    token: ByteVec,
    mut tokenAmount: U256
) {
    event BattleCreate(nftOne: ByteVec, creator: Address, wagerToken: ByteVec, wager: U256)
    event BattleStart(nftOne: ByteVec, nftTwo: ByteVec, reward: U256, hpOne: U256, hpTwo: U256)
    event BattleEnd(nftOne: ByteVec, nftTwo: ByteVec, winner: ByteVec)
    event BattleLeave(nftOne: ByteVec, nftLeft: ByteVec, who: Address, nftWon: ByteVec, winner: Address)
    event BattleAttack(winner: Address, nft: ByteVec, remainingHealth: U256)
    event BattleCancel(nftOne: ByteVec, who: Address)

    enum BattleFactoryErrorCodes {
        NotAdmin = 0
        NotSupercharged = 1
    }

    // helper functions
    // --------------------------------------------------------------------------------------------
    fn isSupercharged(nft: ByteVec) -> Bool {
        // returns a boolean
        return GamifyProtocol(gameFi).isSupercharged(nft)
    }

    // variable amount of PACA
    fn assignReward() -> U256 {
        let seed = u256From32Byte!(
            blake2b!(toByteVec!(blockTarget!()) ++ 
            toByteVec!(blockTimeStamp!()) ++
            toByteVec!(oracle.getValue(b`BTC/USD`).value))
        )
        return addModN!(50, seed, 150)
    } 

    pub fn getWagerDetails(nft: ByteVec) -> (ByteVec, U256) {
        let details = Battle(subContractId!(nft))

        let wagerToken = details.getWagerToken()
        let wagerAmount = details.getWagerAmount()

        return wagerToken, wagerAmount
    }

    // contract functions
    // --------------------------------------------------------------------------------------------
    // notes:
    // battle take place on land plots? ~ could be sorta cool I think
    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn createBattle(nft: ByteVec, wagerToken: ByteVec, amount: U256) -> () {
        // assert the nft has been through gameFi
        assert!(isSupercharged(nft) == true, BattleFactoryErrorCodes.NotSupercharged)

        // value declaration
        let path = nft
        let nullToken = b``

        let (encodeImmutableFields, encodeMutableFields) = Battle.encodeFields!(callerAddress!(), nft, wagerToken, amount, callerAddress!(), nullToken, 1, oracle, gameFi)

        // APS!
        let _ = createSubContract!{callerAddress!() -> ALPH: minimalContractDeposit!(), nft: 1, wagerToken: amount} 
        (
            path,   // use the owners nft as the path to the subContract
            pvp,
            encodeImmutableFields,
            encodeMutableFields
        )

        // emit the event
        emit BattleCreate(nft, callerAddress!(), wagerToken, amount)
    }

    @using(checkExternalCaller = false, preapprovedAssets = true)
    pub fn start(nft: ByteVec, opponent: ByteVec) -> () {
        // assert the nft has been through gameFi
        assert!(isSupercharged(nft) == true, BattleFactoryErrorCodes.NotSupercharged)

        // declare the subContract
        let battle = Battle(subContractId!(opponent))

        // get reward
        let wagerToken = battle.getWagerToken() 
        let amount = battle.getWagerAmount()

        let reward = assignReward()

        // get stats
        let nftOne = GamifyProtocol(gameFi).getNFTstats(nft)
        let nftTwo = GamifyProtocol(gameFi).getNFTstats(opponent)

        // make call to the subContract
        battle.acceptBattle{callerAddress!() -> nft: 1, wagerToken: amount, token: reward}(nft, callerAddress!())

        // emit the event ~ reward is assigned at runtime (oracle)
        emit BattleStart(nft, opponent, reward, nftOne.hp, nftTwo.hp)
    }

    @using(checkExternalCaller = false)
    pub fn attack(nft: ByteVec) -> () {
        let battle = Battle(subContractId!(nft))

        // consider using these in event logging
        let (winner, selectNFT, remainingHealth) = battle.attack()

        // emit
        emit BattleAttack(winner, selectNFT, remainingHealth)
    }

    @using(checkExternalCaller = false)
    pub fn leavebattle(nft: ByteVec) -> () {
        let battle = Battle(subContractId!(nft))

        let (playerOne, nftOne, playerTwo, nftTwo) = battle.getBattleDetails()
        let caller = callerAddress!()

        battle.leave(caller)

        if (caller == playerOne) {
            emit BattleLeave(nft, nftOne, playerOne, nftTwo, playerTwo)
        }
        else {
            emit BattleLeave(nft, nftTwo, playerTwo, nftOne, playerOne)
        }
    }

    @using(checkExternalCaller = false)
    pub fn cancel(nft: ByteVec) -> () {
        let battle = Battle(subContractId!(nft))

        battle.cancel()

        // emit
        emit BattleCancel(nft, callerAddress!())
    }

    @using(checkExternalCaller = false, assetsInContract = true, preapprovedAssets = true, updateFields = true)
    pub fn fund(who: Address, id: ByteVec, amount: U256, topup: Bool) -> () {
        checkCaller!(callerAddress!() == admin, BattleFactoryErrorCodes.NotAdmin)

        if (topup) {
            transferToken!(callerAddress!(), selfAddress!(), id, tokenAmount)

            tokenAmount = tokenAmount + amount
        }

        else {
            transferToken!(selfAddress!(), who, id, tokenAmount)
        }
    }

    @using(checkExternalCaller = true)
    pub fn upgrade(newCode: ByteVec) -> () {
        checkCaller!(callerAddress!() == admin, BattleFactoryErrorCodes.NotAdmin)

        migrate!(newCode)
    }

    @using(checkExternalCaller = true, updateFields = true)
    pub fn upgradeFields(newCode: ByteVec, immutable: ByteVec, mutable: ByteVec) -> () {
        checkCaller!(callerAddress!() == admin, BattleFactoryErrorCodes.NotAdmin)

        migrateWithFields!(newCode, immutable, mutable)
    }

}

TxScript CreatePvp (
    market: BattleFactory,
    paca: ByteVec,
    wagerToken: ByteVec,
    wager: U256
) {
    market.createBattle{callerAddress!() -> ALPH: minimalContractDeposit!()}(paca, wagerToken, wager)
}

TxScript Start (
    market: BattleFactory,
    nft: ByteVec,
    opponent: ByteVec
) {
    let (token, amount) = market.getWagerDetails(nft)
    market.start{callerAddress!() -> token: amount}(nft, opponent)
}

TxScript Attack (
    market: BattleFactory,
    nft: ByteVec

) {
    market.attack(nft)
}

TxScript LeaveBattle (
    market: BattleFactory,
    nft: ByteVec
) {
    market.leavebattle(nft)
}

TxScript Cancel (
    market: BattleFactory,
    nft: ByteVec
) {
    market.cancel(nft)
}

// upgrades

TxScript UpgradeBattleFactory (
    market: BattleFactory,
    newCode: ByteVec
) {
    market.upgrade(newCode)
}

TxScript UpgradeBattleFactoryFields (
    market: BattleFactory,
    newCode: ByteVec,
    immutable: ByteVec,
    mutable: ByteVec
) {
    market.upgradeFields(newCode, immutable, mutable)
}