struct DIAOracleValue {
    mut value: U256,
    mut timestamp: U256
}
  
Interface IDIAOracle {
    pub fn getValue(key: ByteVec) -> DIAOracleValue
}

// implement the supercharged nfts with this

Contract Battle (
    playerOne: Address,
    nftOne: ByteVec,
    // wager
    wagerToken: ByteVec,
    amount: U256,
    // opponent
    mut playerTwo: Address,
    mut nftTwo: ByteVec,
    // battle logic
    mut turn: U256,                 // 1 (one) , 2 (two) ~ binary
    oracle: IDIAOracle,             // random
    gamefi: ByteVec
    //timeSinceLastTurn: U256
) {
    enum BattleCodes {
        InvalidPlayer = 0
        BattleIsActive = 1
        NotYourTurn = 2
        NFTFainted = 3
        BattleNotDone = 4
    }

    // get functions
    pub fn getWagerToken() -> (ByteVec) {
        return wagerToken
    }

    pub fn getWagerAmount() -> (U256) {
        return amount
    }

    pub fn getBattleDetails() -> (Address, ByteVec, Address, ByteVec) {
        return playerOne, nftOne, playerTwo, nftTwo
    }

    // random -> will need a non oracle compatible contract for proper unit testing
    fn random() -> (U256) {
        // add btc and alph; random if or else statement for further random certainity
        let seed = u256From32Byte!(
            blake2b!(toByteVec!(blockTarget!()) ++ 
            toByteVec!(blockTimeStamp!()) ++
            toByteVec!(oracle.getValue(b`BTC/USD`).value))
        )
        return addModN!(5, seed, 10)
    }

    // end battle
    @using(updateFields = false, assetsInContract = true)
    fn endbattle(winner: Address) -> () {
        destroySelf!(winner)
    }

    // swap turns (False = P1, True = P2)
    @using(updateFields = true)
    fn whoTurn() -> (Address, ByteVec, ByteVec) {
        if (turn == 1) {
            turn = 2
            return playerOne, nftOne, nftTwo
        }
        turn = 1
        return playerTwo, nftTwo, nftOne
    }

    // accept the battle when seen from the search
    @using(updateFields = true, checkExternalCaller = false, preapprovedAssets = true)
    pub fn acceptBattle(nft: ByteVec, caller: Address) -> () {
        // assert that battle is not attempted to be joined after started
        assert!(playerTwo == playerOne, BattleCodes.BattleIsActive) 

        // assert that the nft is valid (valid health)
        let stats = GamifyProtocol(gamefi).getNFTstats(nft)

        assert!(stats.hp > 0, BattleCodes.NFTFainted)

        // fetch wager (if present)
        if (amount > 0) {
            transferToken!(caller, selfAddress!(), wagerToken, amount)
        }

        playerTwo = callerAddress!()
        nftTwo = nft
    }

    // attack
    @using(checkExternalCaller = true, updateFields = false)
    pub fn attack() -> (Address, ByteVec, U256) {
        assert!(playerTwo == playerOne, BattleCodes.InvalidPlayer) 

        // checks player
        let (player, nft, opponent) = whoTurn()
        checkCaller!(callerAddress!() == player, BattleCodes.NotYourTurn)

        let stats = GamifyProtocol(gamefi).getNFTstats(nft)
        let damage = stats.atk + random()

        // call the remove health (attack)
        let remainingHealth = GamifyProtocol(gamefi).removeHealth(opponent, damage)

        // is the nft alive
        if (remainingHealth == 0) {
            endbattle(player)

            return player, nft, 0
        }

        return nullContractAddress!(), opponent, remainingHealth
    }

    // leave the battle
    @using(checkExternalCaller = false, updateFields = false, assetsInContract = true)
    pub fn leave(caller: Address) -> () {

        if (caller == playerOne || caller == playerTwo) {
            // compare stats
            //let oneStats = GamifyProtocol(gamefi).getNFTstats(nftOne)
            //let twoStats = GamifyProtocol(gamefi).getNFTstats(nftTwo)

            if (caller == playerOne) {
                destroySelf!(playerTwo) 
            }
            else {
                destroySelf!(playerOne)
            }
        }
    }

    // force cancel / cancel battle
    @using(checkExternalCaller = true, assetsInContract = true)
    pub fn cancel() -> () {
        checkCaller!(playerTwo == playerOne, BattleCodes.BattleNotDone)

        destroySelf!(playerOne)
    }
}

// debugging
TxScript ForceContractCancel (
    pvp: Battle
) {
    pvp.cancel()
}