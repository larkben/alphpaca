struct PairInfo {
    // need to have decimals for liquidation
    pair: ByteVec,
    mut decimals: U256 
}

Contract LoanFactoryTest (
    admin: Address,
    loanTemplate: ByteVec,
    marketTemplate: ByteVec,
    mut activeLoans: U256,
    mut rate: U256,
    oracle: TestOracle
) {
    // price mappings
    mapping [ByteVec, PairInfo] tokenPairs

    // events
    event NewLoan(contract: ByteVec, tokenRequested: ByteVec, tokenAmount: U256, collateralToken: ByteVec, collateralAmount: U256, interest: U256, duration: U256, who: Address)
    event AcceptedLoan(contract: ByteVec, who: Address)
    event LoanRemoved(contract: ByteVec, who: Address)
    event LoanWithdraw(contract: ByteVec, forfeit: Bool, who: Address)
    event LoanLiquidation(contract: ByteVec, token: ByteVec, startingBid: U256)

    // loanee market
    event MarketCreated(contract: ByteVec, token: ByteVec, amount: U256, minInterest: U256, maxTime: U256, liquidation: Bool, who: Address)
    event MarketUpdated(contract: ByteVec, token: ByteVec, amount: U256, minInterest: U256, maxTime: U256, liquidation: Bool)
    event MarketDestroyed(contract: ByteVec, who: Address)

    enum LoanCodes {
        NotAdmin = 0
        TokenSizeTooSmall = 1
    }

    // health / collateral value calculation
    fn determineCollateralRatio(
        tokenRequested: ByteVec,
        tokenAmount: U256,
        collateralToken: ByteVec, 
        collateralAmount: U256,
        threshhold: U256
    ) -> (U256, Bool) {
        if (tokenPairs.contains!(tokenRequested) == true && tokenPairs.contains!(collateralToken)) {
            let mut loan_value = oracle.getValue(tokenPairs[tokenRequested].pair).value

            let mut collateral_value = oracle.getValue(tokenPairs[collateralToken].pair).value

            if (loan_value == 0) {
                return 0, false
            }

            let collateral_decimals = tokenPairs[collateralToken].decimals
            let token_decimals = tokenPairs[tokenRequested].decimals

            let token_scale = 10 ** (token_decimals)
            let collateral_scale = 10 ** (collateral_decimals)

            // Normalize values to the higher decimal scale
            if (token_decimals > collateral_decimals) {
                let scale_factor = 10 ** (token_decimals - collateral_decimals)
                loan_value = (loan_value * tokenAmount) / token_scale
                collateral_value = (collateral_value * scale_factor * collateralAmount) / collateral_scale
            } 
            else if (token_decimals < collateral_decimals) {
                let scale_factor = 10 ** (collateral_decimals - token_decimals)
                loan_value = (loan_value * scale_factor * tokenAmount) / token_scale
                collateral_value = (collateral_value * collateralAmount) / collateral_scale
            } 
            else {
                // No scaling needed if decimals are equal
                loan_value = (loan_value * tokenAmount) / token_scale
                collateral_value = (collateral_value * collateralAmount) / collateral_scale
            }
        
            let collateral_ratio = (collateral_value * 100) / loan_value

            if (collateral_ratio < threshhold) {
                return collateral_ratio, true
            }

            else {
                return collateral_ratio, false
            }
        }

        return 0, false
    }

    pub fn getRequiredTokens(contractId: ByteVec, withInterest: Bool) -> (ByteVec, U256) {
        let loan = LoanTest(contractId)

        let (token, amount) = loan.getTokenRequested()

        if (withInterest) {
            let interestTotal = loan.getInterest()

            return token, interestTotal
        }
        else {
            return token, amount
        }
    }

    fn checkTokenAmount(priceIn: U256, commissionRateIn: U256) -> () {
        // Calculate the commission based on the rate
        let commission = (priceIn * commissionRateIn) / 10000

        // Ensure the token amount is sufficient
        assert!(priceIn > commission, LoanCodes.TokenSizeTooSmall)

        // Additional optional check for a minimum base token amount
        assert!(priceIn >= 10000, LoanCodes.TokenSizeTooSmall)
    }

    // create loans (creator)
    // ---------------------------------------------------------------------------------------
    @using(checkExternalCaller = false, preapprovedAssets = true, updateFields = true)
    pub fn createLoan(
        tokenRequested: ByteVec, 
        tokenAmount: U256, 
        collateralToken: ByteVec, 
        collateralAmount: U256, 
        interest: U256, 
        duration: U256, 
        canLiquidate: Bool
    ) -> () {
        let caller = callerAddress!()

        checkTokenAmount(tokenAmount, rate)

        let (ratio, condition) = determineCollateralRatio(
            tokenRequested, 
            tokenAmount, 
            collateralToken, 
            collateralAmount, 
            150
        )

        let (encodeImmutableFields, encodeMutableFields) = LoanTest.encodeFields!(
            caller,
            nullContractAddress!(),
            tokenRequested,
            tokenAmount,
            // collateral
            collateralToken,
            collateralAmount,
            interest,
            rate,
            duration,
            0,
            false,
            selfAddress!(),
            canLiquidate,
            false,
            ratio,
            nullContractAddress!(),
            tokenAmount,
            0,
            oracle
        )

        let contractID = copyCreateContract!{caller -> ALPH: (minimalContractDeposit!() * 2), collateralToken: collateralAmount}(
            loanTemplate,                                                                                  
            encodeImmutableFields,
            encodeMutableFields
        )

        activeLoans = activeLoans + 1

        // emit event
        emit NewLoan(contractID, tokenRequested, tokenAmount, collateralToken, collateralAmount, interest, duration, caller)
    }

    @using(checkExternalCaller = false, preapprovedAssets = true, updateFields = true)
    pub fn acceptMarket(
        tokenRequested: ByteVec, 
        tokenAmount: U256, 
        collateralToken: ByteVec, 
        collateralAmount: U256, 
        interest: U256, 
        duration: U256,
        loaneeMarket: ByteVec
    ) -> () {
        let caller = callerAddress!()
        let market = LoaneeMarket(loaneeMarket)

        checkTokenAmount(tokenAmount, rate)

        let (loanee, min, int, liquid) = market.getLoaneeDetails()
        let (token, amount) = market.getLoaneeTokenDetails()

        assert!(tokenRequested == token, 2)
        assert!(amount > tokenAmount, 3)
        assert!(interest >= int, 4)
        assert!(duration <= min, 5)

        market.delegate(caller, tokenAmount)

        let (ratio, condition) = determineCollateralRatio(
            tokenRequested, 
            tokenAmount, 
            collateralToken, 
            collateralAmount, 
            150
        )

        let (encodeImmutableFields, encodeMutableFields) = LoanTest.encodeFields!(
            caller,
            loanee,
            tokenRequested,
            tokenAmount,
            // collateral
            collateralToken,
            collateralAmount,
            interest,
            rate,
            duration,
            0,
            true,
            selfAddress!(),
            liquid,
            false,
            ratio,
            nullContractAddress!(),
            tokenAmount,
            0,
            oracle
        )

        let contractID = copyCreateContract!{caller -> ALPH: (minimalContractDeposit!() * 2), collateralToken: collateralAmount}(
            loanTemplate,                                                                                  
            encodeImmutableFields,
            encodeMutableFields
        )

        activeLoans = activeLoans + 1

        // emit event
        emit NewLoan(contractID, tokenRequested, tokenAmount, collateralToken, collateralAmount, interest, duration, caller)
    }

    @using(checkExternalCaller = false, preapprovedAssets = true)
    pub fn accept(contractId: ByteVec) -> () {
        let loan = LoanTest(contractId)

        let caller = callerAddress!()

        let (token, amount) = loan.getTokenRequested()

        loan.acceptLoan{caller -> token: amount}(caller)

        // emit event
        emit AcceptedLoan(contractId, caller)
    }

    @using(checkExternalCaller = false, preapprovedAssets = false)
    pub fn cancel(contractId: ByteVec) -> () {
        let loan = LoanTest(contractId)

        let caller = callerAddress!()

        loan.cancelLoan(caller)

        // emit event
        emit LoanRemoved(contractId, caller)
    }

    @using(checkExternalCaller = false, preapprovedAssets = true, assetsInContract = enforced)
    pub fn pay(contractId: ByteVec) -> () {
        let loan = LoanTest(contractId)

        let caller = callerAddress!()

        let (token, amount) = loan.getTokenRequested()
        let totalAmount = loan.getInterest()

        loan.payLoan{caller -> token: totalAmount}(caller)

        // emit event
        emit LoanWithdraw(contractId, false, caller)
    }
    
    // liquidation
    @using(checkExternalCaller = false, preapprovedAssets = false, assetsInContract = enforced)
    pub fn forfeit(contractId: ByteVec) -> () {
        let loan = LoanTest(contractId)

        let caller = callerAddress!()

        //let (token, amount) = loan.getTokenCollateral()

        loan.acceptForfeit()

        // emit event
        emit LoanWithdraw(contractId, true, caller)
    }

    // check liquidation
    @using(checkExternalCaller = false, updateFields = false)
    pub fn liquidation(contractId: ByteVec) -> () {
        let loan = LoanTest(contractId)

        let caller = callerAddress!()

        let (token, amount) = loan.getTokenRequested()
        let (collateral, collateralAmount) = loan.getTokenCollateral()

        // get liquidation threshold from loan (could be a cool feature)

        let (ratio, liquidate) = determineCollateralRatio(token, amount, collateral, collateralAmount, 150)

        assert!(liquidate == true, 8)

        loan.activateLiquidation(ratio)

        emit LoanLiquidation(contractId, token, amount)
    }

    // auction functions
    @using(checkExternalCaller = false, updateFields = false, preapprovedAssets = true)
    pub fn bid(contractId: ByteVec, bidAmount: U256) -> () {
        let loan = LoanTest(contractId)

        let caller = callerAddress!()

        let (token, _) = loan.getTokenRequested()
        
        let highestBid = loan.getHighestBid()

        assert!(bidAmount > highestBid, 9)

        let timeEnd = loan.bid{caller -> token: bidAmount}(caller, bidAmount)

        // ... emit liquidation
        // ... emit newPotential time end
    }

    @using(checkExternalCaller = false, updateFields = false, preapprovedAssets = false)
    pub fn redeem(contractId: ByteVec) -> () {
        let loan = LoanTest(contractId)

        let caller = callerAddress!()

        loan.redeem(caller)

        // ... emit redeem
    }

    // add collateral function

    // loanee market functions - editMinInt, editMaxTime, canLiquidate, add, withdraw, destroy 
    // --------------------------------------------------------------------------------------
    @using(checkExternalCaller = false, preapprovedAssets = true, updateFields = false)
    pub fn loaneeMarket(
        token: ByteVec,
        tokenAmount: U256,
        // specs
        minInterest: U256,
        maxTime: U256,
        liquidation: Bool
    ) -> () {
        let caller = callerAddress!()

        let (encodeImmutableFields, encodeMutableFields) = LoaneeMarket.encodeFields!(
            caller,
            token,
            tokenAmount,
            minInterest,
            maxTime,
            liquidation,
            selfAddress!()
        )

        let contractID = copyCreateContract!{caller -> token: tokenAmount, ALPH: minimalContractDeposit!() * 3}(
            marketTemplate,                                                                                  
            encodeImmutableFields,
            encodeMutableFields
        )

        // emit event
        emit MarketCreated(contractID, token, tokenAmount, minInterest, maxTime, liquidation, caller)
    }

    @using(checkExternalCaller = false, updateFields = false, preapprovedAssets = false)
    pub fn editInterest(contractId: ByteVec, newInterest: U256) -> () {
        let market = LoaneeMarket(contractId)

        let caller = callerAddress!()

        market.editMinInterest(caller, newInterest)

        let (token, newAmount) = market.getLoaneeTokenDetails()
        let (_, min, int, l) = market.getLoaneeDetails()

        emit MarketUpdated(contractId, token, newAmount, min, int, l)
    }

    @using(checkExternalCaller = false, updateFields = false, preapprovedAssets = false)
    pub fn editTime(contractId: ByteVec, newTime: U256) -> () {
        let market = LoaneeMarket(contractId)

        let caller = callerAddress!()

        market.editMaxTime(caller, newTime)

        let (token, newAmount) = market.getLoaneeTokenDetails()
        let (_, min, int, l) = market.getLoaneeDetails()

        emit MarketUpdated(contractId, token, newAmount, min, int, l)
    }

    @using(checkExternalCaller = false, updateFields = false, preapprovedAssets = false)
    pub fn canLiquidate(contractId: ByteVec, liquid: Bool) -> () {
        let market = LoaneeMarket(contractId)

        let caller = callerAddress!()

        market.editLiquidation(caller, liquid)

        let (token, newAmount) = market.getLoaneeTokenDetails()
        let (_, min, int, l) = market.getLoaneeDetails()

        emit MarketUpdated(contractId, token, newAmount, min, int, l)
    }

    @using(checkExternalCaller = false, updateFields = false, preapprovedAssets = true)
    pub fn addFunds(contractId: ByteVec, token: ByteVec, amount: U256, gas: Bool) -> () {
        let market = LoaneeMarket(contractId)

        let caller = callerAddress!()

        let mut totalALPH = dustAmount!()

        if (gas) {
            totalALPH = totalALPH + minimalContractDeposit!()
        }

        market.add{caller -> token: amount, ALPH: totalALPH}(caller, amount, gas)

        let (_, newAmount) = market.getLoaneeTokenDetails()
        let (_, min, int, liquid) = market.getLoaneeDetails()

        emit MarketUpdated(contractId, token, newAmount, min, int, liquid)
    }

    @using(checkExternalCaller = false, updateFields = false, preapprovedAssets = false)
    pub fn withdrawFunds(contractId: ByteVec, token: ByteVec, amount: U256) -> () {
        let market = LoaneeMarket(contractId)

        let caller = callerAddress!()

        market.withdraw(caller, amount)

        let (_, newAmount) = market.getLoaneeTokenDetails()
        let (_, min, int, liquid) = market.getLoaneeDetails()

        emit MarketUpdated(contractId, token, newAmount, min, int, liquid)
    }

    @using(checkExternalCaller = false, updateFields = false, preapprovedAssets = false)
    pub fn destroyMarket(contractId: ByteVec) -> () {
        let market = LoaneeMarket(contractId)

        let caller = callerAddress!()

        market.destroy(caller)

        emit MarketDestroyed(contractId, caller)
    }

    // admin functions
    // --------------------------------------------------------------------------------------
    @using(checkExternalCaller = true, preapprovedAssets = false, updateFields = true)
    pub fn editRate(newRate: U256) -> () {
        checkCaller!(callerAddress!() == admin, LoanCodes.NotAdmin)

        rate = newRate
    }

    // ... complete this
    @using(checkExternalCaller = true, preapprovedAssets = true, updateFields = false)
    pub fn tokenMapping(token: ByteVec, add: Bool, pairtoken: ByteVec, decimals: U256) -> () {
        checkCaller!(callerAddress!() == admin, LoanCodes.NotAdmin)

        if (add) {
            let b = PairInfo {
                pair: pairtoken,
                decimals: decimals
            }

            tokenPairs.insert!(callerAddress!(), token, b)
        }
        else {
            tokenPairs.remove!(callerAddress!(), token)
        }
    }

    @using(checkExternalCaller = true, preapprovedAssets = false, updateFields = false, assetsInContract = true)
    pub fn destroyLoanFactory() -> () {
        checkCaller!(callerAddress!() == admin, LoanCodes.NotAdmin)

        destroySelf!(admin)

        // emit event
    }

    // withdraw fees
    @using(checkExternalCaller = true, preapprovedAssets = false, updateFields = false, assetsInContract = true)
    pub fn withdrawLoanFactoryFees(who: Address, token: ByteVec, amount: U256) -> () {
        checkCaller!(callerAddress!() == admin, LoanCodes.NotAdmin)

        transferToken!(selfAddress!(), who, token, amount)

        // emit event
    }

    // upgrade
    @using(updateFields = false, checkExternalCaller = true)
    pub fn updateLoanFactoryCode(newCode: ByteVec) -> () {
        checkCaller!(callerAddress!() == admin, LoanCodes.NotAdmin)

        migrate!(newCode)
    }

    @using(updateFields = true, checkExternalCaller = true)
    pub fn updateLoanFactoryFields(newCode: ByteVec, immFields: ByteVec, mutFields: ByteVec) -> () {
        checkCaller!(callerAddress!() == admin, LoanCodes.NotAdmin)

        migrateWithFields!(newCode, immFields, mutFields)
    } 
}

TxScript CreateLoanTest (
    loanFactory: LoanFactoryTest,
    tokenRequested: ByteVec,
    tokenAmount: U256,
    collateralToken: ByteVec,
    collateralAmount: U256,
    interest: U256,
    duration: U256,
    canLiquidate: Bool
) {
    loanFactory.createLoan{callerAddress!() -> ALPH: (minimalContractDeposit!() * 2), collateralToken: collateralAmount}(
        tokenRequested,
        tokenAmount,
        collateralToken,
        collateralAmount,
        interest,
        duration,
        canLiquidate
    )
}

TxScript AcceptMarketTest (
    loanFactory: LoanFactoryTest,
    tokenRequested: ByteVec, 
    tokenAmount: U256, 
    collateralToken: ByteVec, 
    collateralAmount: U256, 
    interest: U256, 
    duration: U256,
    loaneeMarket: ByteVec
) {
    loanFactory.acceptMarket{callerAddress!() -> ALPH: (minimalContractDeposit!() * 2), collateralToken: collateralAmount}(
        tokenRequested,
        tokenAmount,
        collateralToken,
        collateralAmount,
        interest, 
        duration,
        loaneeMarket
    )
}

TxScript AcceptLoanTest (
    loanFactory: LoanFactoryTest,
    contract: ByteVec
) {
    let (token, amount) = loanFactory.getRequiredTokens(contract, false)

    loanFactory.accept{callerAddress!() -> token: amount}(contract)
}

TxScript CancelLoanTest (
    loanFactory: LoanFactoryTest,
    contract: ByteVec
) {
    loanFactory.cancel(contract)
}

TxScript PayLoanTest (
    loanFactory: LoanFactoryTest,
    contract: ByteVec
) {
    let (token, amount) = loanFactory.getRequiredTokens(contract, true)

    loanFactory.pay{callerAddress!() -> token: amount}(contract)
}

TxScript ForfeitLoanTest (
    loanFactory: LoanFactoryTest,
    contract: ByteVec
) {
    loanFactory.forfeit(contract)
}

// liquidation and auctions

TxScript LiquidationLoanTest (
    loanFactory: LoanFactoryTest,
    contract: ByteVec
) {
    loanFactory.liquidation(contract)
}

TxScript BidLoanTest (
    loanFactory: LoanFactoryTest,
    contract: ByteVec,
    bidAmount: U256,
    token: ByteVec
) {
    loanFactory.bid{callerAddress!() -> token: bidAmount}(contract, bidAmount)
}

TxScript RedeemLoanTest (
    loanFactory: LoanFactoryTest,
    contract: ByteVec
) {
    loanFactory.redeem(contract)
}

// create market

TxScript CreateLoaneeMarketTest (
    loanFactory: LoanFactoryTest,
    token: ByteVec,
    tokenAmount: U256,
    minInterest: U256,
    maxTime: U256,
    liquidation: Bool
) {
    loanFactory.loaneeMarket{callerAddress!() -> token: tokenAmount, ALPH: minimalContractDeposit!() * 3}(
        token,
        tokenAmount,
        minInterest,
        maxTime,
        liquidation
    )
}

TxScript EditInterestTest (
    loanFactory: LoanFactoryTest,
    contractId: ByteVec,
    newInterest: U256
) {
    loanFactory.editInterest(contractId, newInterest)
}

TxScript EditTimeTest (
    loanFactory: LoanFactoryTest,
    contractId: ByteVec,
    newTime: U256
) {
    loanFactory.editTime(contractId, newTime)
}

TxScript EditLiquidateTest (
    loanFactory: LoanFactoryTest,
    contractId: ByteVec,
    liquid: Bool
) {
    loanFactory.canLiquidate(contractId, liquid)
}

TxScript AddFundsTest (
    loanFactory: LoanFactoryTest,
    contractId: ByteVec,
    token: ByteVec,
    amount: U256,
    gas: Bool
) { 

    let mut minimalALPH = dustAmount!()
    if (gas) {
        minimalALPH = minimalALPH + minimalContractDeposit!()
    }

    loanFactory.addFunds{callerAddress!() -> token: amount, ALPH: minimalALPH}(contractId, token, amount, gas)
}

TxScript WithdrawFundsTest (
    loanFactory: LoanFactoryTest,
    contractId: ByteVec,
    token: ByteVec,
    amount: U256
) {
    loanFactory.withdrawFunds(contractId, token, amount)
}

TxScript DestroyMarketTest (
    loanFactory: LoanFactoryTest,
    contractId: ByteVec
) {
    loanFactory.destroyMarket(contractId)
}

// admin

TxScript EditLoanRateTest (
    loanFactory: LoanFactoryTest,
    newRate: U256
) {
    loanFactory.editRate(newRate)
}

TxScript WithdrawLoanFactoryFeesTest (
    loanFactory: LoanFactoryTest,
    who: Address,
    token: ByteVec,
    amount: U256
) {
    loanFactory.withdrawLoanFactoryFees(who, token, amount)
}

TxScript TokenMapping (
    loanFactory: LoanFactoryTest,
    token: ByteVec, 
    add: Bool,
    pairtoken: ByteVec,
    decimals: U256
) {
    loanFactory.tokenMapping{callerAddress!() -> ALPH: mapEntryDeposit!()}(token, add, pairtoken, decimals)
}

// upgrade / destroy

TxScript UpdateLoanFactoryCodeTest (
    loanFactory: LoanFactoryTest,
    newCode: ByteVec
) {
    loanFactory.updateLoanFactoryCode(newCode)
}

TxScript UpdateLoanFactoryFieldsTest (
    loanFactory: LoanFactoryTest,
    newCode: ByteVec, 
    immFields: ByteVec, 
    mutFields: ByteVec
) {
    loanFactory.updateLoanFactoryFields(newCode, immFields, mutFields)
}

TxScript DestroyLoanFactoryTest (
    loanFactory: LoanFactoryTest
) {
    loanFactory.destroyLoanFactory()
}

