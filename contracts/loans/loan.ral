Contract Loan (
    creator: Address,
    mut loanee: Address,
    tokenRequested: ByteVec,
    tokenAmount: U256,
    // collateral
    collateralToken: ByteVec,
    collateralAmount: U256,
    interest: U256,
    rate: U256,                 // 200 would be 2%
    duration: U256,
    mut startTime: U256,
    // active
    mut active: Bool,
    parentContract: Address
) {

    // helper functions
    pub fn getTokenRequested() -> (ByteVec, U256) {
        return tokenRequested, tokenAmount
    }

    pub fn getTokenCollateral() -> (ByteVec, U256) {
        return collateralToken, collateralAmount
    }

    pub fn isLoanComplete() -> (Bool) {
        assert!(active == true, 1)

        // confirm the loan has completed it's time
        assert!(blockTimeStamp!() > startTime + duration, 0)

        return true
    }

    pub fn getInterest(principal: U256) -> (U256) {
        if (interest > 0) {
            // Calculate the proportional interest based on elapsed time
            let timeFactor = (blockTimeStamp!() - startTime) / 31556926000
    
            // Calculate the gain for the elapsed time
            let gain = (principal * interest * timeFactor) / 10000
    
            // Return the original amount plus interest
            return principal + gain
        }
        else {
            return principal
        }
    }

    fn getCommission(total: U256) -> (U256) {
        if (rate > 0) {
            let comission = (total * rate) / 10000
            // return the original amount plus interest
            return comission 
        }
        else {
            return 0
        }
    }       

    // contract functions
    @using(checkExternalCaller = true, updateFields = true, preapprovedAssets = true)
    pub fn acceptLoan(acceptor: Address) -> () {
        checkCaller!(callerAddress!() == parentContract, 0)

        assert!(active == false, 4)

        // transfer tokens to person requesting them
        transferToken!(acceptor, creator, tokenRequested, tokenAmount)

        // assignment
        active = true
        startTime = blockTimeStamp!()
        loanee = acceptor
    }

    @using(checkExternalCaller = true, assetsInContract = true)
    pub fn cancelLoan(caller: Address) -> () {
        checkCaller!(callerAddress!() == parentContract, 0)

        // confirm loan is not running
        assert!(active == false, 1)

        destroySelf!(caller)
    }

    @using(checkExternalCaller = true, assetsInContract = true)
    pub fn forceCancel(caller: Address) -> () {
        // confirm loan is not running
        assert!(active == false, 1)

        destroySelf!(caller)
    }

    @using(checkExternalCaller = true, preapprovedAssets = true, assetsInContract = true)
    pub fn payLoan(caller: Address) -> () {
        checkCaller!(callerAddress!() == parentContract, 0)

        // payback the loan
        assert!(active == true, 2)

        // transfer back assets plus the interest
        let totalPayment = getInterest(tokenAmount)

        let totalCommission = getCommission(totalPayment)

        // user pays back two seperate accounts (marketplace fees, and the person who loaned them the assets)
        transferToken!(caller, loanee, tokenRequested, totalPayment - totalCommission)

        transferToken!(caller, parentContract, tokenRequested, totalCommission)

        // destroy the contract
        destroySelf!(creator)
    }

    @using(checkExternalCaller = true, preapprovedAssets = false, assetsInContract = true)
    pub fn acceptForfeit() -> () {
        checkCaller!(callerAddress!() == parentContract, 0)

        assert!(active == true, 3)
        assert!(startTime + duration < blockTimeStamp!(), 4)

        let totalCommission = getCommission(collateralAmount)
        transferToken!(selfAddress!(), parentContract, collateralToken, totalCommission)

        // claim collateral
        destroySelf!(loanee)
    }
}

TxScript ForceCancel (
    loan: Loan
) {
    loan.forceCancel(callerAddress!())
}